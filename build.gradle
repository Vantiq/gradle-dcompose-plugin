group 'com.chrisgahlert'

if(project.hasProperty('publishVersion') && publishVersion) {
    version = publishVersion
}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
        classpath 'gradle.plugin.com.chrisgahlert:gradle-dcompose-plugin:0.2.2'
    }
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.chrisgahlert.gradle-dcompose-plugin'

repositories {
    mavenCentral()
}

sourceCompatibility = targetCompatibility = 1.6

configurations {
    all {
        resolutionStrategy {
            force "org.codehaus.groovy:groovy-all:${GroovySystem.getVersion()}"
        }
    }
}

dependencies {
    compile gradleApi()

    if(GradleVersion.current().compareTo(GradleVersion.version('2.6')) < 0) {
        testCompile 'com.netflix.nebula:nebula-test:2.2.2'
    } else if(GradleVersion.current().compareTo(GradleVersion.version('2.8')) < 0) {
        testCompile 'com.netflix.nebula:nebula-test:3.1.0'
    } else {
        testCompile 'com.netflix.nebula:nebula-test:4.0.0'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

dcompose {
    dockerDaemon {
        image = 'docker:1.11.0-dind'
        privileged = true
        portBindings = ['2375']
    }
}

test {
    maxParallelForks = Math.round(Runtime.runtime.availableProcessors() / 2)

    doFirst {
        def dockerHost;
        if(System.getenv('DOCKER_HOST') == null || System.getenv('DOCKER_HOST').startsWith('unix://')) {
            dockerHost = 'localhost'
        } else {
            dockerHost = new URI(System.getenv('DOCKER_HOST')).host
        }

        def dockerPort = dcompose.dockerDaemon.findHostPort(2375)

        jvmArgs "-DDOCKER_HOST=tcp://$dockerHost:$dockerPort", '-DDOCKER_TLS_VERIFY=0'
    }

    dependsOn startDockerDaemonContainer
    finalizedBy removeDockerDaemonContainer
}

pluginBundle {
    website = vcsUrl = 'http://github.com/chrisgahlert/gradle-dcompose-plugin'
    description = 'Plugin for automatically managing Docker container lifecycles in Gradle. This plugin was inspired by the gradle-docker-plugin from Benjamin Muschko and by docker-compose'
    tags = ['docker', 'management', 'application', 'container', 'lifecycle', 'compose']

    plugins {
        dcomposePlugin {
            id = 'com.chrisgahlert.gradle-dcompose-plugin'
            displayName = 'Gradle dcompose Plugin'
        }
    }
}
publishPlugins.dependsOn test
